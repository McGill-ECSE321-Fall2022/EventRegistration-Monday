plugins {
	id 'org.springframework.boot' version '2.7.3'
	id 'io.spring.dependency-management' version '1.0.13.RELEASE'
	id 'java'
	// Code coverage tool
	id 'jacoco'
	// Postman tests (to run them, uncomment this and run gradle postman)
	// id 'com.github.michaelruocco.gradle-postman-runner' version '0.1.4'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	// Nice API docs
	implementation 'org.springdoc:springdoc-openapi-ui:1.6.6'
}

tasks.named('test') {
	useJUnitPlatform()
	testLogging {
		exceptionFormat "full"
	}
	// Generate the coverage report whenever the tests are run
	finalizedBy 'jacocoTestReport'
}

tasks.named('jacocoTestReport') {
	// The tests must be run before the coverage report can be generated
	dependsOn 'test'
}

// Enforce minimum instruction coverage goal
jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.8
			}
		}
	}
}
